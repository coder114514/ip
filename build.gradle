plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}

repositories {
    mavenCentral()
}

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'
}

test {
    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'

        showExceptions true
        exceptionFormat 'full'
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    applicationName = 'tobtahc'
    mainClass.set('tobtahc.Main')
}

jar {
    enabled = false
}

distZip.enabled = false
distTar.enabled = false
installDist.enabled = false
startScripts.enabled = false

distributions {
    shadow {
        distributionBaseName = 'tobtahc'
    }
}

shadowJar {
    archiveBaseName = 'tobtahc'
    archiveClassifier = ''
}

tasks.named('startShadowScripts') {
    outputDir = file("${buildDir}/scripts")
}

run {
    standardInput = System.in
}

checkstyle {
    toolVersion = '11.0.0'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
}

build.dependsOn javadoc
