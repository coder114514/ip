plugins {
    id 'java'
    id 'application'
    id 'checkstyle'
    id 'com.github.johnrengelman.shadow' version '7.1.2'
    id 'org.openjfx.javafxplugin' version '0.1.0'
}

repositories {
    mavenCentral()
}

def os = org.gradle.internal.os.OperatingSystem.current()
def javaFxPlatform = os.isWindows() ? 'win' : os.isMacOsX() ? 'mac' : 'linux'

dependencies {
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: '5.10.0'
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: '5.10.0'

    String javaFxVersion = '17.0.7'

    implementation group: 'org.openjfx', name: 'javafx-base', version: javaFxVersion, classifier: javaFxPlatform
    implementation group: 'org.openjfx', name: 'javafx-controls', version: javaFxVersion, classifier: javaFxPlatform
    implementation group: 'org.openjfx', name: 'javafx-fxml', version: javaFxVersion, classifier: javaFxPlatform
    implementation group: 'org.openjfx', name: 'javafx-graphics', version: javaFxVersion, classifier: javaFxPlatform
}

javafx {
    version = '17'
    modules = [ 'javafx.base', 'javafx.controls', 'javafx.fxml', 'javafx.graphics' ]
}

test {
    useJUnitPlatform()

    testLogging {
        events 'passed', 'skipped', 'failed'

        showExceptions true
        exceptionFormat 'full'
        showCauses true
        showStackTraces true
        showStandardStreams = false
    }
}

application {
    applicationName = 'tobtahc'
    mainClass.set('tobtahc.Main')
}

jar {
    enabled = false
}

tasks.matching { it.name in ['distZip',
                             'distTar',
                             'installDist',
                             'startScripts',
                             'shadowDistZip',
                             'shadowDistTar',
                             'installShadowDist',
                             'startShadowScripts'] }
        .configureEach { enabled = false }

shadowJar {
    archiveBaseName = 'tobtahc'
    archiveClassifier = javaFxPlatform
}

run {
    standardInput = System.in
}

checkstyle {
    toolVersion = '11.0.0'
}

javadoc {
    source = sourceSets.main.allJava
    classpath = sourceSets.main.compileClasspath
}

build.dependsOn javadoc
